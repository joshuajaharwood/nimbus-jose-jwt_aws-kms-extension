plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'jacoco'
    id "com.vanniktech.maven.publish" version "0.36.0"
    id 'signing'
}

group = 'com.joshuaharwood'
version = '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

javadoc {
    options.tags = [
            "apiNote:a:API Note:",
            "implSpec:a:Implementation Requirements:",
            "implNote:a:Implementation Note:"
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    api "org.jspecify:jspecify:1.0.0"

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'com.nimbusds:nimbus-jose-jwt:10.7'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // These dependencies is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'software.amazon.awssdk:kms:2.41.27'
    implementation 'commons-codec:commons-codec:1.21.0'

    implementation 'com.google.guava:guava:33.5.0-jre'

    implementation platform('org.junit:junit-bom:5.14.2')
    implementation platform('org.slf4j:slf4j-bom:2.0.17')

    implementation 'org.slf4j:slf4j-api'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.slf4j:slf4j-simple'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.assertj:assertj-core:3.27.7'

    testImplementation platform("org.mockito:mockito-bom:4.11.0")

    // Mockito
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-inline'
    testImplementation 'org.mockito:mockito-junit-jupiter'

    // Easy Rand
    testImplementation 'org.jeasy:easy-random-core:4.3.0'
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates 'com.joshuaharwood', 'nimbus-jose-jwt_aws-kms-extension', '2.0.0'

    pom {
        name = 'AWS KMS Extension Fork, for Nimbus JOSE JWT'
        description = 'This library package is a fork of an extension of nimbus-jose-jwt library. ' +
                'It provides JWE based encrypters/decrypters and JWS based signers/verifiers ' +
                'for doing operations with cryptographic keys stores in AWS KMS.'
        url = 'https://github.com/joshuajaharwood/nimbus-jose-jwt_aws-kms-extension'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        scm {
            connection = 'scm:git:ssh://github.com/joshuajaharwood/nimbus-jose-jwt_aws-kms-extension.git'
            url = 'https://github.com/joshuajaharwood/nimbus-jose-jwt_aws-kms-extension'
            tag = "v$version"
        }
        developers {
            developer {
                id = 'joshuajaharwood'
                name = 'Joshua J. A. Harwood'
                email = 'me@joshuaharwood.com'
            }
            developer {
                id = 'lynx-tech'
                name = 'Lync Tech'
                email = 'lynx-tech@amazon.com'
            }
        }
    }
}

test {
    useJUnitPlatform() // Use JUnit Platform for unit tests.
}

// JoCoCo config
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    'com.nimbusds.jose.crypto.*',
                    'com.nimbusds.jose.aws.kms.exceptions.*',
                    'com.nimbusds.jose.aws.kms.scripts.*',
            ]
            limit {
                counter = 'BRANCH'
                // todo: add constructor tests to get back to 1.00
                minimum = 0.50
            }
            limit {
                counter = 'LINE'
                minimum = 0.50
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
