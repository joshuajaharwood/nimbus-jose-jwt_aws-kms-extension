plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group = 'com.joshuaharwood'
version = '2.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    api "org.jspecify:jspecify:1.0.0"

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    // TODO: 9.32 onwards adds AAD support. Once implemented in our implementations, we can jump to 10.x - JH
    api 'com.nimbusds:nimbus-jose-jwt:10.7'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // These dependencies is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'software.amazon.awssdk:kms:2.41.10'
    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'commons-codec:commons-codec:1.18.0'

    implementation 'com.google.guava:guava:[32,)'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.assertj:assertj-core:[3,4)'

    // Mockito
    testImplementation 'org.mockito:mockito-core:[3,4)'
    testImplementation 'org.mockito:mockito-inline:[3,4)'
    testImplementation 'org.mockito:mockito-junit-jupiter:[3,4)'

    // Easy Rand
    testImplementation 'org.jeasy:easy-random-core:4.3.0'
}

apply from: 'scripts.gradle'
apply from: 'maven.gradle'

test {
    useJUnitPlatform() // Use JUnit Platform for unit tests.
}

// JoCoCo config
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    'com.nimbusds.jose.crypto.*',
                    'com.nimbusds.jose.aws.kms.exceptions.*',
                    'com.nimbusds.jose.aws.kms.scripts.*',
            ]
            limit {
                counter = 'BRANCH'
                minimum = 1.00
            }
            limit {
                counter = 'LINE'
                minimum = 1.00
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
